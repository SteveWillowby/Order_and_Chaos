#!/bin/sh
USAGE="$0 /home/wayne/src/bionets/SANA/resnik/taxons/[Species-of-interest] clusters.align gene2go.file
The clusters in the clusters.align has the following properties:
    - one cluster per line
    - exactly as many tab-separated columns as species were specified on the taxon command line, in the same order
    - empty columns (ie., if a species has no proteins in thi cluster) represented by one underscore '_'
    - if multiple proteins from the same species are in the cluster, they are space-separated within the tab-separated column 
Output: the same line, preceded by K(C) (not necessarily in the same order), and then a final line with the total knowledge, and then the K(A) of the 'alignment' specified by the clusters file."

die() { echo "$@" >&2; exit 1
}
die "This one kinda words, but shouldn't be used. Use the official one: NetGO.awk in the NetGO sub-repo."
minW=0
minK=0
minA=0

while :; do
    case "$1" in
    -min[wW]*) minW=$2; shift 2;;
    -min[kK]*) minK=$2; shift 2;;
    -min[aA]*) minA=$2; shift 2;;
    -*) die "unknown option '$1'" ;;
    *) break ;;
    esac
done

hawk -T 'BEGIN{numSpecies=ARGC-3}
    match(FILENAME, "[A-Z][A-Z].*.txt$"){
	# record species names and taxonomic IDs
	F=FILENAME; gsub(".*/","",F);gsub(".txt$","",F);
	shortName=substr(F,1,2);
	name2id[F]=$1; name2id[shortName]=$1;
	ourTaxIDs[$1]=F; # taxonomic IDs of interest from the gene2go file
	taxID2col[$1]=ARGIND; # which column is this species in the cluster.align file?
	col2taxIDs[ARGIND][$1]=1;
	next; # skip rest of the script
    }
    !/GO:/&&!/tax_id.*PubMed/{ # assume a cluster line
	ASSERT(NF==numSpecies,"cluster file "FILENAME", line "FNR" should have "numSpecies" tab-separated column but has "NF);
	clusterSize[FNR]=0;
	numClusters=FNR; # whatever the last value we get is correct
	for(c=1;c<=NF;c++) if($c=="_") {
		C[FNR][c][0]=1; delete C[FNR][c][0]; # make C[FNR][c] an explicit empty set
	    }
	    else {
		n=split($c,a," "); # split the column on spaces
		clusterSize[FNR]+=n;
		for(i=1;i<=n;i++) {
		    C[FNR][c][a[i]]=1; # define member proteins of this species in this cluster
		    P[c][a[i]]=1; # mark this protein in this species as one whose GO terms we need.
		}
	    }
	next; # skip rest of the script
    }
    FNR>1&& 	# ignore top (header) line of gene2go file
	$1 in ourTaxIDs {
	col = taxID2col[$1]
	if($2 in P[col]) { # and only consider GO terms that annotate proteins that interest us
	    ++GOfreq[col][$3] # increase the frequency of this GO term by 1 in this species
	    pGO[col][$2][$3]=1 # keep track of the GO terms that annotate this protein in this species
	    ++globalGOfreq[$3];
	}
    }
    END{
	# Create K_s(p) for each protein in each species
	minNumGOs=1e30
	maxNumGOs=totalNumGOs=0
	for(c=1;c<=numSpecies;c++){
	    numGOs = length(GOfreq[c])
	    if(numGOs < minNumGOs)minNumGOs=numGOs
	    if(numGOs > maxNumGOs)maxNumGOs=numGOs
	    totalNumGOs += numGOs

	    for(u in P[c])
		if(u in pGO[c]) for(g in pGO[c][u]) K[c][u]+=1/GOfreq[c][g]
		else{
		    K[c][u]=0;
		    pGO[c][u][1]=0; delete pGO[c][u][1] # make pGO[c][u] an empty set
		}
	}
	totalK = 0;
	for(cl=1;cl<=numClusters;cl++){
	    delete numP # this will count the number of proteins annotated by each GO term that appears in this cluster
	    delete maxGOfreq
	    delete minGOfreq
	    #count frequency of each GO term in this cluster:
	    for(c in C[cl])for(u in C[cl][c]) if(K[c][u]>='$minK') for(g in pGO[c][u]){
		++numP[g]
		if(GOfreq[c][g] > maxGOfreq[g]) maxGOfreq[g]=GOfreq[c][g];
		if(g in minGOfreq) {
		    if(GOfreq[c][g] < minGOfreq[g]) minGOfreq[g]=GOfreq[c][g];
		} else minGOfreq[g] = GOfreq[c][g];
	    }
	    totalGOs=length(numP) # total number of GO terms that appear in this cluster
	    # At this point, if we are as Draconian as possible, the only GO terms that are in the intersection across
	    # all proteins are those for which numP[g] = clusterSize[cl], ie, this GO term appeared in every protein.
	    # We allow less Draconian interpretations by allowing minK>0.
	    K_C = 0;
	    for(g in numP) if(numP[g] >= clusterSize[cl] - '$minA') K_C += numP[g]/maxGOfreq[g];
	    totalK += K_C
	    printf "%g", K_C
	    for(c in C[cl]){printf "\t"; for(u in C[cl][c]) printf " %s",u}
	    print ""
	}
	printf "%g %d/%d/%d %d %g (total common knowledge, min/max/totalNumGOs, numClust, similarity K/minNumGOs)\n",
	    totalK, minNumGOs,maxNumGOs,totalNumGOs, length(C), totalK/totalNumGOs;
	printf "The numbers are too low between mammals, and too high between distant species like SC-HS. In the NetGO paper they were about 0.8 and 0.05, which sounds a lot better." > "/dev/fd/2"
    }' "$@"
